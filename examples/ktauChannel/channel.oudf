@kernel void drag(const dlong Nelements,
		  const dlong offset,
		  const dlong soffset,
		  const dfloat mue,
		  const dlong biD,
		  @ restrict const dfloat *sgeo,
		  @ restrict const dlong *vmapM,
		  @ restrict const int *EToBM,
		  @ restrict const dfloat *invLMM,
		  @ restrict const dfloat *sij,		  
		  @ restrict dfloat *drag,
		  @ restrict dfloat *area)
{
  for (dlong e = 0; e < Nelements; e++; @outer(0)) {
    
    for (int f = 0; f < p_Nfaces; f++) {
      for (int m = 0; m < p_Nfp; ++m; @inner(0)) {
	const int n = m + f * p_Nfp;
	const dlong sid = e * p_Nfaces * p_Nfp + n;
	const dlong idM = vmapM[sid];
        const dlong biDM = EToBM[f + p_Nfaces * e];

	const dfloat sWJ = sgeo[sid * p_Nsgeo + p_WSJID];
	const dfloat n1 = sgeo[sid * p_Nsgeo + p_NXID] * sWJ;
	const dfloat n2 = sgeo[sid * p_Nsgeo + p_NYID] * sWJ;
	const dfloat n3 = sgeo[sid * p_Nsgeo + p_NZID] * sWJ;
		
	if(biDM == biD){
	  const dfloat s11 = sij[idM + 0 * offset];
	  const dfloat s21 = sij[idM + 3 * offset];
	  const dfloat s31 = sij[idM + 5 * offset];

	  const dfloat s12 = sij[idM + 3 * offset];
	  const dfloat s22 = sij[idM + 1 * offset];
	  const dfloat s32 = sij[idM + 4 * offset];

	  const dfloat s13 = sij[idM + 5 * offset];
	  const dfloat s23 = sij[idM + 4 * offset];
	  const dfloat s33 = sij[idM + 2 * offset];

	  const dfloat scale = mue * invLMM[idM];

	  drag[sid + 0 * soffset] = -scale * (s11*n1 + s12*n2 + s13*n3);
	  drag[sid + 1 * soffset] = -scale * (s21*n1 + s22*n2 + s23*n3);
	  drag[sid + 2 * soffset] = -scale * (s31*n1 + s32*n2 + s33*n3);
	  area[sid] = sWJ; 
	}
      }
      @barrier();
    }
  }
}

@kernel void scalarScaledAdd(const dlong N,
                             const dfloat a,
                             const dfloat b,
                             @restrict const dfloat* X,
                             @restrict dfloat* Y)
{
  for(dlong n = 0; n < N; ++n; @tile(256,@outer,@inner))
    if(n < N)
      Y[n] = a + b * X[n];
}

void scalarDirichletConditions(bcData *bc)
{
  bc->s = 0;
  if(bc->scalarId == 0) bc->s = 0; 
}
